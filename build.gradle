subprojects {
    buildscript {
        ext.kotlinVersion = '1.3.10'
        ext.ktlintVersion = '0.8.1'

        repositories {
            mavenCentral()
            google()
            maven { url "https://plugins.gradle.org/m2/" }
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:3.2.1'
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
            classpath 'net.ltgt.gradle:gradle-apt-plugin:0.10'
            classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
            classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
        }
    }

    repositories {
        mavenCentral()
        google()
    }

    group ARTIFACT_GROUP
    version ARTIFACT_VERSION

    apply plugin: 'checkstyle'

    checkstyle {
        toolVersion = '8.14'
        configFile rootProject.file('checkstyle.xml')
        showViolations true
        configProperties = ['checkstyle.cache.file': rootProject.file('build/checkstyle.cache')]
    }

    task checkstyle(type: Checkstyle) {
        source 'src'
        include '**/*.java'
        classpath = files()
    }

    def configureKtlint = {
        project.configurations {
            ktlint
        }

        project.dependencies {
            ktlint 'com.github.shyiko:ktlint:0.8.2'
        }

        def ktlintCheck = project.tasks.create('ktlintCheck', JavaExec.class) {
            main = 'com.github.shyiko.ktlint.Main'
            classpath = project.configurations.ktlint
            args 'src/**/*.kt'
        }

        project.check.dependsOn ktlintCheck

        project.tasks.create('ktlintFormat', JavaExec.class) {
            main = 'com.github.shyiko.ktlint.Main'
            classpath = project.configurations.ktlint
            args '-F', 'src/**/*.kt'
        }
    }
    project.plugins.withId('kotlin', configureKtlint)
}
